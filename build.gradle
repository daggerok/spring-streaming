buildscript {
  ext {
    environmentFileDir = 'env'
    springBootVersion = '1.5.15.RELEASE'
    propertiesPluginEnvironmentNameProperty = 'env'
  }
}

plugins {
  id 'java'
  id 'idea'
  id 'eclipse'
  id 'net.saliman.properties' version '1.4.6'
  id 'com.github.ben-manes.versions' version '0.15.0'
  id 'io.franzbecker.gradle-lombok' version '1.14' apply false
  id 'org.springframework.boot' version '1.5.15.RELEASE' apply false
  id 'io.spring.dependency-management' version '1.0.6.RELEASE' apply false
}

allprojects {

  apply plugin: 'java'

  version '2.0.0'
  group 'com.github.daggerok'
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
  defaultTasks 'clean', 'build'

  repositories {
    mavenCentral()
    jcenter()
  }

  apply plugin: 'io.franzbecker.gradle-lombok'
  lombok.version = '1.18.2'

  apply plugin: 'io.spring.dependency-management'

  dependencyManagement {
    imports {
      mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
    }
  }

  apply plugin: 'org.springframework.boot'

  dependencies {
    compile 'org.springframework.boot:spring-boot-starter'
    runtime 'org.springframework.boot:spring-boot-devtools'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
  }

  // gradle bootRun -Dserver.context-path=/app
  bootRun { systemProperties = System.properties }

  springBoot {
    executable = true
  }
}

dependencies {
  compile project(':web'), project(':user'), project(':admin'),
      'org.springframework.boot:spring-boot-starter-websocket'
}

wrapper {
  gradleVersion = '4.10'
  distributionType = 'BIN'
}

bootRepackage {
  enabled = false
}
